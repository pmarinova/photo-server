plugins {
    id 'application'
    id 'distribution'
    id 'org.graalvm.buildtools.native' version '0.10.2'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation libs.com.google.code.gson.gson
    implementation libs.org.jmdns.jmdns
    implementation libs.commons.cli.commons.cli
    implementation libs.org.slf4j.slf4j.api
    implementation libs.org.slf4j.slf4j.simple
}

application {
    mainClass = 'pm.photos.server.PhotoServerApp'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

def nativeImageBuildArgs = [
    '-H:IncludeResourceBundles=sun.net.httpserver.simpleserver.resources.simpleserver',
    '--install-exit-handlers'
]

if (hasProperty('static')) {
    nativeImageBuildArgs.add('--static')
    nativeImageBuildArgs.add('--libc=musl')
}

graalvmNative {
    binaries {
        main {
            buildArgs.addAll(nativeImageBuildArgs)
        }
    }
}

distributions {
    winsw {
        contents {
            from(nativeCompile.outputFile)
            from('service/windows')
            from('.') {
                include('photos/**')
            }
        }
    }
}